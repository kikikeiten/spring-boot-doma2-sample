bootJar {
    enabled = false
}

jar {
    enabled = true
}

// Copy the resources referred by the Doma annotation processors to
// the destinationDir of the compileJava task
task copyDomaResources(type: Sync)  {
    from sourceSets.main.resources.srcDirs
    into compileJava.destinationDir
    include "doma.compile.config"
    include "META-INF/**/*.sql"
    include "META-INF/**/*.script"
}

compileJava {
    // Depend on the above task
    dependsOn copyDomaResources
    options.encoding = "UTF-8"
}

compileTestJava {
    options.encoding = "UTF-8"
    // Disable the annotation processors during the test run
    options.compilerArgs = ["-proc:none"]
}

dependencies {
    compile project(":sample-common")

    // springframework
    compile "org.springframework.boot:spring-boot-starter-aop"
    compile "org.springframework.boot:spring-boot-starter-validation"
    compile "org.springframework.boot:spring-boot-starter-mail"
    compile "org.springframework.boot:spring-boot-starter-thymeleaf"
    compile "org.springframework.boot:spring-boot-starter-jdbc"
    compile "org.springframework.boot:spring-boot-starter-json"

    // doma exclude springframework
    annotationProcessor "org.seasar.doma.boot:doma-spring-boot-starter:1.5.0"
    compile("org.seasar.doma.boot:doma-spring-boot-starter:1.1.1") {
        exclude group: "org.springframework.boot"
    }

    // thymeleaf
    compile "org.codehaus.groovy:groovy:${groovyVersion}"
    compile("nz.net.ultraq.thymeleaf:thymeleaf-layout-dialect:2.5.3") {
        exclude group: "org.codehaus.groovy", module: "groovy"
        exclude group: "org.thymeleaf", module: "thymeleaf"
    }

    // jackson
    compile "com.fasterxml.jackson.dataformat:jackson-dataformat-csv"

    // modelmapper
    compile "org.modelmapper:modelmapper:2.4.3"

    // mysql database
    compile "mysql:mysql-connector-java"
    compile "org.flywaydb:flyway-core"

    testCompile "org.springframework.boot:spring-boot-starter-test"
    testCompile "org.spockframework:spock-spring:${spockVersion}"
}
